buildscript {
	ext {
		springBootPlugin = 'org.springframework.boot:spring-boot-gradle-plugin'
		springBootVersion = '2.1.4.RELEASE'
		thinPlugin = 'org.springframework.boot.experimental:spring-boot-thin-gradle-plugin'
		thinVersion = '1.0.11.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("${springBootPlugin}:${springBootVersion}")
		classpath("${thinPlugin}:${thinVersion}")
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
//add tasks thinJar and thinResolve for thin JAR deployments
apply plugin: 'org.springframework.boot.experimental.thin-launcher'


group = 'com.gomezrondon'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
	mavenCentral()
}

ext {
	set('springCloudVersion', 'Greenwich.SR1')
}

dependencies {
	implementation 'org.springframework.cloud:spring-cloud-starter-function-web'// solo for testing loca
	implementation 'org.springframework.cloud:spring-cloud-function-adapter-aws'
	implementation 'com.amazonaws:aws-lambda-java-core:1.2.0'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}


springBoot {
	mainClassName = 'com.gomezrondon.springawsgateway.Application'
}

bootJar {
//    This is overridden by the mainClassName in springBoot{} and added here for reference purposes.
	mainClassName = 'com.gomezrondon.springawsgateway.Application'

}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

//Enable this to generate and use a pom.xml file
apply plugin: 'maven'

//If you want to customize the generated pom.xml you can edit this task and add it as a dependency to the bootJar task
task createPom {
	def basePath = 'build/resources/main/META-INF/maven'
	doLast {
		pom {
			withXml(dependencyManagement.pomConfigurer)
		}.writeTo("${basePath}/${project.group}/${project.name}/pom.xml")
	}
}
//Uncomment the following to use your custom generated pom.xml
bootJar.dependsOn = [createPom]

//Enable this to generate and use a thin.properties file
//bootJar.dependsOn = [thinProperties]